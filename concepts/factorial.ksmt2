%parser {
  ;; = and : are predefined
  ;; precedence levels are a linked enum
  group Range [after Assignment] infix ltr
  symbols {
    Range ..
    Arithmetic1 - +
    Arithmetic2 * /
    Comparison <
  }
}

container functional {
  def permutation(n r: Int) = product n - r + 1 .. n
  def factorial(n: Int) = permutation n n
  def combination(n r: Int) = div (permutation n r) (factorial r)
}

container imperative {
  def factorial(n: Int) {
    x = i = 1
    while i < n {
      x = x * (i = i + 1)
    }
    x
  }

  def permutation(n r: Int) {
    x = 1
    i = next n - r
    while i < n {
      x |>= * (i |>= + 1)
    }
    x
  }

  def combination(n r: Int) (
    let (result x = 1, i = 0) (
      while i < r {
        x = x * (n - i) / (r - i)
        incr i
      }))
}

assert_is (functional.factorial 5) (imperative.factorial 5) 120
assert_is functional.permutation(4, 2) (imperative.permutation 4 2) 12
assert_is (functional.combination 6 3) (imperative.combination 6 3) 20